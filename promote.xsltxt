stylesheet 1.0;

output method=xml indent="no";

include die.xsl;

// The target constructor kind
#kind = { ""; }

// The target constructor nr
#nr = { ""; }

// The target constructor relnr
#relnr = { ""; }

// The target constructor's article
#aid = { ""; }

tpl [*] {
  $element = `local-name ()`;
  if [*] {
    <$element {
      for-each [@*] { copy-of `.`; }
      "\n";
      for-each [*] {
        apply [.]; "\n";
      }
    }
  } else {
    <$element {
      for-each [@*] { copy-of `.`; }
    }
  }
}

tpl [/] {
  apply [Constructors];
}

tpl escape-backslash(#str) {
  if [contains ($str, '/')] {
    $before = `substring-before ($str, '/')`;
    $after = `substring-after ($str, '/')`;
    $tail = escape-backslash (#str = $after);
    `concat ($before, "&amp;#47;", $tail)`;
  } else {
    $str;
  }
}

tpl [Constructors] {
  <Constructors {
    @aid = `@aid`;
    @mizfiles = escape-backslash (#str = `@mizfiles`);
    "\n";
    for-each [Constructor] {
      apply [.]; "\n";
    }
  }
}

// Promote a bundle of argument types to a bundle of 'set' types
tpl [ArgTypes] mode="promote" {
  <ArgTypes {
    for-each [*] {
      apply [.] mode="promote";
      "\n";
    }
  }
}

tpl [Properties] mode="promote" {
  <Properties; // empty list of properties
}

// Promote a type to the topmost type 'set'
tpl [Typ] mode="promote" {
  <Typ {
    @kind = "M";
    @nr = "1";
    "\n";
    <Cluster;
    "\n";
  }
}

tpl [Constructor[@kind = "M"]] {
  <Constructor {
    for-each [@*] { copy-of `.`; }
    if [$kind = "M" and $nr = @nr and @relnr = $relnr and @aid = $aid] {
      @promoted = "yes";
      "\n";
      apply [Properties] mode="promote";
      "\n";
      apply [ArgTypes] mode="promote"; // argument types
      "\n";
      apply [Typ] mode="promote";
      "\n";
    } else {
      @promoted = "no";
      "\n";
      for-each [*] {
        apply [.]; "\n";
      }
    }
  }
}

// For right functor brackets, there is nothing to do
tpl [Constructor[@kind = "L"]] {
  <Constructor {
    for-each [@*] { copy-of `.`; }
    if [$kind = "L"] {
      msg terminate="no" "Promoting a right functor bracket; there is nothing to do.";
      @promoted = "yes";
    } else {
      @promoted = "no";
    }
    "\n";
    for-each [*] {
      apply [.]; "\n";
    }
  }
}

tpl [Constructor[@kind = "G"]] {
  <Constructor {
    for-each [@*] { copy-of `.`; }
    if [$kind = "G" and @nr = $nr and @relnr = $relnr and @aid = $aid] {
      @promoted = "yes";
      "\n";
      apply [ArgTypes] mode="promote"; // argument types
      "\n";
      // result type: "mere structure".  We might also experiment with making the return type 'set'.  But this might screw up too much.
      <Typ {
        @kind = "G";
        @nr = "1";
        <Cluster;
        "\n";
      }
      "\n";
      apply [Fields]; // nothing to do here
      "\n";
    } else {
      @promoted = "no";
      "\n";
      for-each [*] {
        apply [.]; "\n";
      }
    }
  }
}

tpl [Constructor[@kind = "U"]] {
  <Constructor {
    for-each [@*] { copy-of `.`; }
    if [$kind = "U" and @nr = $nr and @relnr = $relnr and @aid = $aid] {
      @promoted = "yes";
      "\n";
      apply [ArgTypes] mode="promote"; // argument types
      "\n";
      apply [Typ] mode="promote"; // result type
      "\n";
    } else {
      @promoted = "no";
      "\n";
      for-each [*] {
        apply [.]; "\n";
      }
    }
  }
}

tpl [Constructor[@kind = "V"]] {
  <Constructor {
    for-each [@*] { copy-of `.`; }
    if [$kind = "V" and @nr = $nr and @relnr = $relnr and @aid = $aid] {
      @promoted = "yes";
      "\n";
      apply [ArgTypes] mode="promote"; // argument types
      "\n";
    } else {
      @promoted = "no";
      "\n";
      for-each [*] {
        apply [.]; "\n";
      }
    }
  }
}

tpl [Constructor[@kind = "R"]] {
  <Constructor {
    for-each [@*] { copy-of `.`; }
    if [$kind = "R" and @nr = $nr and @relnr = $relnr and @aid = $aid] {
      @promoted = "yes";
      "\n";
      apply [Properties] mode="promote"; // ignore properties
      "\n";
      apply [ArgTypes] mode="promote"; // argument types
      "\n";
    } else {
      @promoted = "no";
      "\n";
      for-each [*] {
        apply [.]; "\n";
      }
    }
  }
}

tpl [Constructor[@kind = "K"]] {
  <Constructor {
    for-each [@*] { copy-of `.`; }
    if [$kind = "K" and @nr = $nr and @relnr = $relnr and @aid = $aid] {
      @promoted = "yes";
      "\n";
      apply [Properties] mode="promote"; // ignore properties
      "\n";
      apply [ArgTypes] mode="promote"; // argument types
      "\n";
      apply [Typ] mode="promote"; // result type
      "\n";
    } else {
      @promoted = "no";
      "\n";
      for-each [*] {
        apply [.]; "\n";
      }
    }
  }
}
