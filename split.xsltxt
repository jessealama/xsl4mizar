stylesheet 1.0;

output method=xml indent="yes";

include die.xsl;

tpl [*] {
  $element = `local-name ()`;
  <$element {
    for-each [@*] { copy-of `.`; }
    apply [*];
  }
}

tpl count-preceding-eliminable-toplevel-items {
  `count (preceding::Item[@kind = "Reservation" or @kind = "Private-Functor-Definition" or @kind = "Choice-Statement" or @kind = "Constant-Definition" or @kind = "Type-Changing-Statement" or @kind = "Private-Predicate-Definition" or (@kind = "Regular-Statement" and child::Diffuse-Statement) or (@kind = "Regular-Statement" and child::Proposition[descendant::Simple-Term/@idnr = preceding::Item[parent::Text-Proper and @kind = "Constant-Definition"]/Variable/@idnr or descendant::Simple-Term/@idnr = preceding::Item[parent::Text-Proper and @kind = "Type-Changing-Statement"]/Equality/Variable/@idnr or descendant::Simple-Term/@idnr = preceding::Item[parent::Text-Proper and @kind = "Type-Changing-Statement"]/Variable/@idnr])] | preceding::Item[@kind = "Loci-Declaration"])`;
}

tpl [Text-Proper/Item[@kind = "Reservation"]] {
  $type = `*[position() = last()]`;
  $num-previous-toplevel = count-preceding-eliminable-toplevel-items ();
  for-each [Variable] {
    <Item {
      for-each [@*] { copy-of `.`; }
      @kind = "Reservation";
      @toplevel-binder-number = `position() + $num-previous-toplevel`;
      if [@idnr] {
        @idnr = `@idnr`;
      } else {
        die (#message = "We found a variable that lacks an idnr attribute; unable to proceed.");
      }
      apply [.];
      apply [$type];
    }
  }
}

tpl [Text-Proper/Item[@kind = "Private-Predicate-Definition" or @kind = "Private-Functor-Definition" or @kind = "Constant-Definition"]] {
  if [Variable[2]] {
    die (#message = "We assume that private predicate/functor definitions bind at most one variable, but we found one that binds at least two!");
  }
  <Item {
    for-each [@*] { copy-of `.`; }
    @toplevel-binder-number = count-preceding-eliminable-toplevel-items ();
    @idnr = `Variable[1]/@idnr`;
    apply [*];
  }
}

tpl [Text-Proper/Item[@kind = "Type-Changing-Statement"]] {
  $num-children = `count (*)`;
  $justification = `*[position() = $num-children]`;
  $num-children-minus-one = `$num-children - 1`;
  $type = `*[position() = $num-children-minus-one]`;
  $num-previous-toplevel = count-preceding-eliminable-toplevel-items ();
  for-each [Equality] {
    $variable = `Variable`;
    $definiens = `*[position() = last()]`;
    <Item {
      for-each [@*] { copy-of `.`; }
      @kind = "Type-Changing-Statement";
      @toplevel-binder-number = `position() + $num-previous-toplevel`;
      <Equality {
        apply [$variable];
        apply [$definiens];
      }
      apply [$type];
      apply [$justification];
    }
  }
}

tpl [Text-Proper/Item[@kind = "Regular-Statement"]] {
  <Item {
    for-each [@*] { copy-of `.`; }
    choose {
      when [Regular-Statement and Proposition/Label] {
        @idnr = `Proposition/Label[1]/@idnr`;
      }
      when [Diffuse-Statement and Label] {
        @idnr = `Label[1]/@idnr`;
      }
      otherwise {
        @idnr = "";
      }
    }
    if [Diffuse-Statement] {
      $num-previous-toplevel = count-preceding-eliminable-toplevel-items ();
      @toplevel-binder-number = `$num-previous-toplevel + 1`;
    }
    apply [*];
  }
}

tpl [Item[@kind = "Constant-Definition"]] {
  <Item {
    for-each [@*] { copy-of `.`; }
    @idnr = `Variable[1]/@idnr`;
    $num-previous-toplevel = count-preceding-eliminable-toplevel-items ();
    @toplevel-binder-number = `$num-previous-toplevel + 1`;
    apply [*];
  }
}

tpl [Item[@kind = "Loci-Declaration"]] {
  $loci = `.`;
  for-each [Implicitly-Qualified-Segment | Explicitly-Qualified-Segment] {
    $segment_element_name = `local-name (.)`;
    for-each [Variable] {
      <Item {
        for-each [$loci/@*] { copy-of `.`; }
        $num_preceding = `count (preceding::Item[@kind = "Loci-Declaration"] | preceding::Item[@kind = "Reservation"] | preceding::Item[@kind = "Private-Functor-Definition" and parent::Text-Proper] | preceding::Item[@kind = "Choice-Statement" and parent::Text-Proper] | preceding::Item[@kind = "Constant-Definition" and parent::Text-Proper] | preceding::Item[@kind = "Type-Changing-Statement" and parent::Text-Proper] | preceding::Item[@kind = "Private-Predicate-Definition" and parent::Text-Proper] | preceding::Item[@kind = "Regular-Statement" and child::Diffuse-Statement and parent::Text-Proper])`;
        @toplevel-binder-number = `$num_preceding + 1`;
        @idnr = `@idnr`;
        <$segment_element_name {
          apply [.]; // variable
          if [$segment_element_name = "Explicitly-Qualified-Segment"] {
            apply [../*[position() = last()]]; // the type of the variable
          }
        }
      }
    }
  }
}

tpl [Item[@kind="Definition-Item"]] {
  $context = `.`;
  apply [Block] (#context = $context);
}

tpl [Block[@kind="Definitional-Block"]] (#context) {
  apply [Item[@kind = "Attribute-Definition" or @kind = "Functor-Definition" or @kind = "Predicate-Definition" or @kind = "Mode-Definition" or @kind = "Structure-Definition"]](#definitions = `Item[@kind = "Private-Predicate-Definition" or @kind="Private-Functor-Definition"]`, #context = $context);
}

tpl [Block[@kind = "Registration-Block"]] {
  apply [Item[@kind = "Cluster"]];
}

tpl [Block[@kind = "Notation-Block"]] {
  apply [Item[@kind = "Pred-Antonym" or @kind = "Pred-Synonym" or @kind = "Attr-Antonym" or @kind = "Attr-Synonym" or @kind = "Func-Synonym" or @kind = "Func-Antonym" or @kind = "Mode-Synonym" or @kind = "Mode-Antonym"]];
}

tpl [Item[@kind = "Cluster"]] {
  <Item {
    @kind = "Definition-Item";
    if [@line] { @line = `@line`; }
    if [@col] { @col = `@col`; }
    <Block {
      @kind = "Registration-Block";
      for-each [preceding-sibling::Item[@kind = "Loci-Declaration"]] {
        apply [.];
      }
      <Item {
        @kind = "Cluster";
        apply [*];
      }
      correctness-conditions (#candidates = `following-sibling::*`);
    }
  }
}

tpl correctness-conditions (#candidates, #definitions, #stop) {
  if [not($stop = "1")] {
    if [$candidates] {
      $candidate = `$candidates[1]`;
      for-each [$candidate] {
        $candidate_name = `local-name(.)`;
        $candidate_kind = `$candidate/@kind`;
        if [$candidate_name = "Item" and ($candidate_kind = "Correctness-Condition" or $candidate_kind = "Property" or $candidate_kind = "Correctness")] {
          apply [$candidate](#definitions = $definitions);
          correctness-conditions (#candidates = `$candidates[position() > 1]`,
                                  #definitions = $definitions);
        } else {
          correctness-conditions (#stop = "1");
        }
      }
    }
  }
}

tpl [Item[@kind = "Correctness-Condition" or @kind = "Property" or @kind = "Correctness"]](#definitions) {
  <Item {
    @kind = `@kind`;
    apply [*](#definitions = $definitions);
  }
}

tpl [Item[@kind = "Theorem-Item"]] {
  <Item {
    for-each [@*] { copy-of `.`; }
    apply [*];
  }
}

tpl [Block[@kind = "Proof"]](#definitions) {
  $context = `.`;
  <Block {
    @kind = "Proof";
    if [$definitions] {
      for-each [$definitions] {
        if [Variable] {
          $idnr = `Variable[1]/@idnr`;
          if [$context//Private-Predicate-Formula[@idnr = $idnr] or $context//Private-Functor-Term[@idnr = $idnr]] {
            copy-of `.`;
          }
        } else {
          die (#message = "While determining whether to possibly include some local definitions into a proof, we encountered a local definition that lacks a Variable child");
        }
      }
    }
    apply [*];
  }
}

tpl [Item[@kind = "Functor-Definition" or @kind = "Attribute-Definition" or @kind = "Predicate-Definition" or @kind = "Mode-Definition" or @kind = "Structure-Definition"]](#definitions, #context) {
  <Item {
    for-each [$context/@*] { copy-of `.`; }
    @kind = "Definition-Item";
    <Block {
      @kind = "Definitional-Block";
      for-each [preceding-sibling::Item[@kind = "Loci-Declaration" or @kind = "Assumption" or @kind = "Existential-Assumption" or @kind = "Constant-Definition"]] {
        apply [.];
      }
      <Item {
        @kind = `@kind`;
        if [@line] { @line = `@line`; }
        if [@col] { @col = `@col`; }
        apply [*];
      }
      correctness-conditions (#candidates = `following-sibling::*`, #definitions = $definitions);
    }
  }
}

tpl [Item[@kind = "Pred-Synonym" or @kind = "Pred-Antonym" or @kind = "Attr-Synonym" or @kind = "Attr-Antonym" or @kind = "Func-Synonym" or @kind = "Func-Antonym" or @kind = "Mode-Synonym" or @kind = "Mode-Antonym"]] {
  <Item {
    @kind = "Definition-Item";
    <Block {
      @kind = "Notation-Block";
      for-each [preceding-sibling::Item[@kind = "Loci-Declaration"]] {
        apply [.];
      }
      <Item {
        @kind = `@kind`;
        if [@line] { @line = `@line`; }
        if [@col] { @col = `@col`; }
        apply [*];
      }
      correctness-conditions (#candidates = `following-sibling::*`);
    }
  }
}

tpl split-explicitly-qualified-segment (#variables, #type) {
  if [$variables] {
    $variable = `$variables[1]`;
    <Explicitly-Qualified-Segment {
      apply [$variable];
      apply [$type];

    }
    split-explicitly-qualified-segment (#variables = `$variables[position() > 1]`,
                                        #type = $type);
  }
}

tpl [Explicitly-Qualified-Segment[not(parent::Item[@kind = "Choice-Statement"])]] {
  split-explicitly-qualified-segment (#variables = `Variable`,
                                      #type = `*[position() = last()]`);
}

// don't break up explicitly qualified segments inside universal formulas...yet
tpl [Universal-Quantifier-Formula/Explicitly-Qualified-Segment] {
  <Explicitly-Qualified-Segment {
    apply [*];
  }
}

tpl [Existential-Quantifier-Formula/Explicitly-Qualified-Segment] {
  <Explicitly-Qualified-Segment {
    apply [*];
  }
}